#include <Arduino.h>
#include <Wire.h>
#include <Adafruit_AS5600.h>
#include <WiFi.h>
#include <HTTPClient.h>

Adafruit_AS5600 as5600;

const char* ssid = "COLOQUEOSEUWIFI";
const char* password = "COLOQUEASUASENHA";

// URL DO GOOGLE SCRIPT QUE FOI CRIADO
String googleURL = "COLOQUEOLINKCRIADODOGOOGLESHEET?";

//VARIÁVEIS GLOBAIS
unsigned long ultimoEnvio = 0; 
float anguloAtual = 0.0;       

void setup() {
  Serial.begin(115200);
  Wire.begin();

  WiFi.begin(ssid, password);

  as5600.begin(); 
}

void loop() {
  //PARTE 1: CHECAGEM
  //VERIFICAÇÃO DE SEGURANÇA
  // Antes de ler, perguntamos: "Tem alguém no endereço 0x36?"
  Wire.beginTransmission(0x36);
  byte erro = Wire.endTransmission();

  if (erro == 0) {
    // SENSOR CONECTADO
    float raw = as5600.getAngle(); 
    
    // Filtro simples para ignorar leitura fantasma de 4095 (359.91)
    // Se der 4095 exato, é erro e força o anterior ou 0
    if (raw >= 4095) {
       // Mantém o anguloAtual como estava ou zera por segurança.
       anguloAtual = 0.0;
    } else {
       anguloAtual = raw * (360.0 / 4096.0);
    }

  } else {
    //SENSOR DESCONECTADO
    //Força a boca a fechar (0 graus) para não bugar o LabVIEW
    anguloAtual = 0.0;
  }

  //PARTE 2: LABVIEW
  if (Serial.available() > 0) {
    while(Serial.available() > 0) Serial.read(); 
    Serial.println(anguloAtual, 0); 
  }

  //PARTE 3: GOOGLE SHEETS
  if (millis() - ultimoEnvio > 2000) { 
     if(WiFi.status() == WL_CONNECTED){
        HTTPClient http;
        // Se o sensor estiver desconectado, envia 0 para o Google
        float aberturaEstimada = 10.0 * (anguloAtual * 3.14159 / 180.0);
        String urlFinal = googleURL + "angulo=" + String(anguloAtual) + "&abertura=" + String(aberturaEstimada);
        
        http.begin(urlFinal.c_str());
        http.setFollowRedirects(HTTPC_STRICT_FOLLOW_REDIRECTS);
        int httpCode = http.GET(); 
        http.end();
     }
     ultimoEnvio = millis(); 
  }
}
